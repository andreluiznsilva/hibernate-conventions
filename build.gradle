buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
        classpath 'net.researchgate:gradle-release:2.0.1'
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.4'
    }
}

apply plugin: 'net.researchgate.release'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'cobertura'

allprojects{
   
    sourceCompatibility = 1.5
    targetCompatibility = 1.5

    repositories {
      jcenter()
    }
    
    dependencies {

        testCompile 'junit:junit:4.11'
        testCompile 'org.hsqldb:hsqldb:2.3.2'
        
        testCompile 'xerces:xercesImpl:2.7.1'

        compile 'org.hibernate:hibernate-entitymanager:4.3.5.Final'
        compile 'commons-cli:commons-cli:1.2'	
        
    }
        
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    
    task copyTestReports(type: Copy) {
         from 'build/test-results'
         into 'shippable/testresults'
         include '**/*.xml'
         includeEmptyDirs = false
    }
    
    task copyCoverageReports(type: Copy) {
        from 'build/reports/cobertura'
        into 'shippable/codecoverage'
        include '**/*.xml'
        includeEmptyDirs = false
    }
    
    publishing {
      publications {
        mavenJava(MavenPublication) {
          artifactId = 'hibernate-conventions'
          if (plugins.hasPlugin('war')) {
              from components.web
          } else {
              from components.java
          }
          artifact sourcesJar 
          artifact javadocJar
        }
      }
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    bintray {
        user = bintray_user         // this usually comes from gradle.properties file in ~/.gradle
        key = bintray_api_key       // this usually comes from gradle.properties file in ~/.gradle
        publications = ['mavenJava']        
        publish = true
        pkg {
            repo = 'maven'
            name = 'hibernate-conventions'
        }
    }
    
    cobertura {
        coverageFormats = ['html', 'xml']
        coverageIgnoreTrivial = true
        coverageCheckTotalBranchRate = 65
        coverageCheckTotalLineRate = 65
    }
    
}